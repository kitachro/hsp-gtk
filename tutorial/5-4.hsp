#const	FALSE	0
#const	TRUE	1

#include "hscallbk.as"
#uselib ""
#func cb_window_delete_event ""
#func cb_button1_toggled ""
#func cb_button2_toggled ""
#func cb_button3_toggled ""

#uselib "libgtk-3-0.dll"
#func global gtk_init "gtk_init" sptr, sptr
#func global gtk_window_new "gtk_window_new" int
#const	GTK_WINDOW_TOPLEVEL	0
#func global gtk_container_set_border_width "gtk_container_set_border_width" sptr, int
#func global gtk_container_add "gtk_container_add" sptr, sptr
#func global gtk_widget_show_all "gtk_widget_show_all" sptr
#func global gtk_main "gtk_main"
#func global gtk_main_quit "gtk_main_quit"
#func global gtk_hbox_new "gtk_hbox_new" int, int
#func global gtk_box_pack_start "gtk_box_pack_start" sptr, sptr, int, int, int
#func global gtk_radio_button_new_with_label_from_widget "gtk_radio_button_new_with_label_from_widget" sptr, sptr
#func global gtk_radio_button_new_with_mnemonic_from_widget "gtk_radio_button_new_with_mnemonic_from_widget" sptr, sptr
#func global gtk_toggle_button_get_active "gtk_toggle_button_get_active" sptr

#uselib "libgobject-2.0-0.dll"
#define g_signal_connect(%1, %2, %3, %4) g_signal_connect_data %1, %2, %3, %4, 0, 0
#func global g_signal_connect_data "g_signal_connect_data" sptr, str, sptr, sptr, int, int

	gtk_init	0, 0

	// set up window
	gtk_window_new	GTK_WINDOW_TOPLEVEL
	win	= stat
	gtk_container_set_border_width	win, 10
	setcallbk	cbwindowdeleteevent, cb_window_delete_event, *on_window_delete_event
	g_signal_connect	win, "delete-event", varptr( cbwindowdeleteevent ), 0

	// set up hbox
	gtk_hbox_new	FALSE, 6
	hbox	= stat

	// set up buttons
	gtk_radio_button_new_with_label_from_widget	0, "Button 1"
	btn1	= stat
	setcallbk	cbbutton1toggled, cb_button1_toggled, *on_button1_toggled
	g_signal_connect	btn1, "toggled", varptr( cbbutton1toggled ), 0

	gtk_radio_button_new_with_label_from_widget	btn1, "_Button 2"
	btn2	= stat
	setcallbk	cbbutton2toggled, cb_button2_toggled, *on_button2_toggled
	g_signal_connect	btn2, "toggled", varptr( cbbutton2toggled ), 0

	gtk_radio_button_new_with_mnemonic_from_widget	btn1, "_Button 3"
	btn3	= stat
	setcallbk	cbbutton3toggled, cb_button3_toggled, *on_button3_toggled
	g_signal_connect	btn3, "toggled", varptr( cbbutton3toggled ), 0

	// build up gui
	gtk_box_pack_start	hbox, btn1, TRUE, TRUE, 0
	gtk_box_pack_start	hbox, btn2, TRUE, TRUE, 0
	gtk_box_pack_start	hbox, btn3, TRUE, TRUE, 0
	gtk_container_add	win, hbox

	// start app
	gtk_widget_show_all	win
	gtk_main
	end

*on_window_delete_event
	gtk_main_quit
	return

*on_button1_toggled
	btn	= btn1
	num_btn	= 1
	gosub	*show_message
	return

*on_button2_toggled
	btn	= btn2
	num_btn	= 2
	gosub	*show_message
	return

*on_button3_toggled
	btn	= btn3
	num_btn	= 3
	gosub	*show_message
	return

*show_message
	gtk_toggle_button_get_active	btn
	if stat {
		text_state	= "ON"
	}
	else {
		text_state	= "OFF"
	}
	mes	"Button " + num_btn + "Ç™" + text_state + "Ç…Ç»ÇËÇ‹ÇµÇΩÅB"
	return
