#const	FALSE	0
#const	TRUE	1

#include "hscallbk.as"
#uselib ""
#func cb_window_delete_event ""
#func cb_button1_clicked ""
#func cb_button2_clicked ""

#uselib "libgtk-3-0.dll"
#func global gtk_init "gtk_init" sptr, sptr
#func global gtk_window_new "gtk_window_new" int
#const	GTK_WINDOW_TOPLEVEL	0
#func global gtk_widget_show_all "gtk_widget_show_all" sptr
#func global gtk_main "gtk_main"
#func global gtk_main_quit "gtk_main_quit"
#func global gtk_button_new_with_label "gtk_button_new_with_label" sptr
#func global gtk_container_add "gtk_container_add" sptr, sptr
#func global gtk_hbox_new "gtk_hbox_new" int, int
#func global gtk_box_pack_start "gtk_box_pack_start" sptr, sptr, int, int, int
#uselib "libgobject-2.0-0.dll"
#define g_signal_connect(%1, %2, %3, %4) g_signal_connect_data %1, %2, %3, %4, 0, 0
#func global g_signal_connect_data "g_signal_connect_data" sptr, str, sptr, sptr, int, int

	gtk_init	0, 0

	gtk_window_new	GTK_WINDOW_TOPLEVEL
	win	= stat
	setcallbk	cbwindowdeleteevent, cb_window_delete_event, *on_window_delete_event
	g_signal_connect	win, "delete-event", varptr( cbwindowdeleteevent ), 0

	gtk_hbox_new	FALSE, 5
	hbox	= stat

	gtk_button_new_with_label	"Hello"
	btn1	= stat
	setcallbk	cbbutton1clicked, cb_button1_clicked, *on_button1_clicked
	g_signal_connect	btn1, "clicked", varptr( cbbutton1clicked ), 0

	gtk_button_new_with_label	"Goodbye"
	btn2	= stat
	setcallbk	cbbutton2clicked, cb_button2_clicked, *on_button2_clicked
	g_signal_connect	btn2, "clicked", varptr( cbbutton2clicked ), 0

	gtk_container_add	win, hbox
	gtk_box_pack_start	hbox, btn1, TRUE, TRUE, 0
	gtk_box_pack_start	hbox, btn2, TRUE, TRUE, 0

	gtk_widget_show_all	win
	gtk_main
	end

*on_window_delete_event
	gtk_main_quit
	return

*on_button1_clicked
	mes	"Hello"
	return

*on_button2_clicked
	mes	"Goodbye"
	return
