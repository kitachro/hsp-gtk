// コールバック関数を使うための準備
#include "modclbk.as"
newclbk3	cb_win_delete_event, 3, *on_win_delete_event, CLBKMODE_CDECL@
newclbk3	cb_btn1_clicked, 2, *on_btn1_clicked, CLBKMODE_CDECL@
newclbk3	cb_btn2_clicked, 2, *on_btn2_clicked, CLBKMODE_CDECL@

// GTK+の関数を使うための準備
#uselib "libgtk-3-0.dll"
#func global gtk_init "gtk_init" sptr, sptr
#func global gtk_window_new "gtk_window_new" int
#const	GTK_WINDOW_TOPLEVEL	0
#func global gtk_widget_show_all "gtk_widget_show_all" sptr
#func global gtk_main "gtk_main"
#func global gtk_main_quit "gtk_main_quit"
#func global gtk_button_new_with_label "gtk_button_new_with_label" sptr
#func global gtk_container_add "gtk_container_add" sptr, sptr
#func global gtk_hbox_new "gtk_hbox_new" int, int
#func global gtk_box_pack_start "gtk_box_pack_start" sptr, sptr, int, int, int

#uselib "libgobject-2.0-0.dll"
#define g_signal_connect(%1, %2, %3, %4) g_signal_connect_data %1, %2, %3, %4, 0, 0
#func global g_signal_connect_data "g_signal_connect_data" sptr, str, sptr, sptr, int, int

// よく使う定数
; ヌルポインタ
#const	NULL	0
; 真偽値
#const	FALSE	0
#const	TRUE	1

	// GTK+初期化
	gtk_init	NULL, NULL

	// ウィンドウ生成
	gtk_window_new	GTK_WINDOW_TOPLEVEL
	win	= stat
	g_signal_connect	win, "delete-event", cb_win_delete_event, NULL

	// HBox生成
	gtk_hbox_new	FALSE, 5
	hbox	= stat

	// ボタン群生成
	gtk_button_new_with_label	"Hello"
	btn1	= stat
	g_signal_connect	btn1, "clicked", cb_btn1_clicked, NULL

	gtk_button_new_with_label	"Goodbye"
	btn2	= stat
	g_signal_connect	btn2, "clicked", cb_btn2_clicked, NULL

	// ウィンドウの組み立て
	gtk_container_add	win, hbox
	gtk_box_pack_start	hbox, btn1, TRUE, TRUE, 0
	gtk_box_pack_start	hbox, btn2, TRUE, TRUE, 0

	// ウィンドウの表示とメインループの開始
	gtk_widget_show_all	win
	gtk_main
	end

/* シグナルハンドラ */
*on_win_delete_event
	gtk_main_quit
	return

*on_btn1_clicked
	mes	"Hello"
	return

*on_btn2_clicked
	mes	"Goodbye"
	return
