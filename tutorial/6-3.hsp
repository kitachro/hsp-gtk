*_GTKの関数を使うための準備
#uselib "libgtk-3-0.dll"
#func global gtk_init "gtk_init" sptr, sptr

#func global gtk_window_new "gtk_window_new" int
#func global gtk_widget_show_all "gtk_widget_show_all" sptr
#func global gtk_container_add "gtk_container_add" sptr, sptr

#func global gtk_grid_new "gtk_grid_new"
#func global gtk_grid_attach "gtk_grid_attach" sptr, sptr, int, int, int, int
#func global gtk_grid_attach_next_to "gtk_grid_attach_next_to" sptr, sptr, sptr, int, int, int

#func global gtk_button_new_with_label "gtk_button_new_with_label" str

#func global gtk_main "gtk_main"
#func global gtk_main_quit "gtk_main_quit"

#uselib "libgobject-2.0-0.dll"
#define g_signal_connect(%1, %2, %3, %4) g_signal_connect_data %1, %2, %3, %4, 0, 0
#func global g_signal_connect_data "g_signal_connect_data" sptr, str, sptr, sptr, int, int

*_コールバック関数を使うための準備
#include "modclbk.as"
	newclbk3	cb_win_delete_event, 3, *on_win_delete_event, CLBKMODE_CDECL@

*_よく使う定数
; ヌルポインタ
#const	NULL	0

*_GTK初期化
	gtk_init	NULL, NULL

*_Window生成
	gtk_window_new	GTK_WINDOW_TOPLEVEL@
	win = stat
	g_signal_connect	win, "delete-event", cb_win_delete_event, NULL

*_Grid生成
	gtk_grid_new
	grd	= stat

*_ボタン群生成
	gtk_button_new_with_label	"Button 1"
	btn1	= stat
	gtk_button_new_with_label	"Button 2"
	btn2	= stat
	gtk_button_new_with_label	"Button 3"
	btn3	= stat
	gtk_button_new_with_label	"Button 4"
	btn4	= stat
	gtk_button_new_with_label	"Button 5"
	btn5	= stat
	gtk_button_new_with_label	"Button 6"
	btn6	= stat

*_ウィンドウ組み立て
#const	GTK_POS_RIGHT	1	; GtkPositionType
#const	GTK_POS_BOTTOM	3
	gtk_container_add	grd, btn1
	gtk_grid_attach		grd, btn2, 1, 0, 2, 1
	gtk_grid_attach_next_to		grd, btn3, btn1, GTK_POS_BOTTOM, 1, 2
	gtk_grid_attach_next_to		grd, btn4, btn3, GTK_POS_RIGHT, 2, 1
	gtk_grid_attach		grd, btn5, 1, 2, 1, 1
	gtk_grid_attach_next_to		grd, btn6, btn5, GTK_POS_RIGHT, 1, 1
	gtk_container_add	win, grd

*_ウィンドウの表示とメインループの開始
	gtk_widget_show_all	win
	gtk_main
	end

*_シグナルハンドラ
*on_win_delete_event
	gtk_main_quit
	return
